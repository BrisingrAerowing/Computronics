buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'forge'

file "build.properties" withReader {
	def prop = new Properties()
	prop.load(it)
	ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft.version}-${config.computronics.version}"
group = "pl.asie.computronics" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Computronics"

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

repositories {
	maven {
		name = "OC repo"
		url = "http://maven.cil.li/"
	}
	maven {
		name "Mobius Repo"
		url "http://mobiusstrip.eu/maven"
	}
	maven {
		name 'CB Repo'
		url "http://chickenbones.net/maven/"
	}
	maven {
		name 'tterrag Repo'
		url "http://maven.tterrag.com/"
	}
	maven {
		name = "Forestry and IC2 repo"
		url = "http://maven.ic2.player.to/"
	}
	maven {
		name = "GregTech"
		url = "http://gregtech.overminddl1.com/"
	}
	maven {
		name = "RX14's Repo"
		url = "http://mvn.rx14.co.uk/repo/"
	}
	ivy {
		name = "BuildCraft"
		artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision]-[classifier].[ext]"
	}
	maven {
		name 'MaryTTS repo'
		url 'http://dl.bintray.com/marytts/marytts'
	}
}

configurations {
	provided
	provided2
	embedded
	compile.extendsFrom provided, provided2, embedded
	all*.exclude group: "codechicken"
}

dependencies {
	compile "li.cil.oc:OpenComputers:${config.oc.version}:dev"
	compile "li.cil.tis3d:TIS-3D:${config.tis3d.version}:dev"
	provided "net.industrial-craft:industrialcraft-2:${config.ic2.version}:dev"
	provided "com.gregoriust.gregtech:gregtech_1.7.10:${config.gregtech.version}:dev"
	provided2 "com.gregoriust.gregtech:gregtech_1.7.10:${config.gregtech6.version}:dev"
	compile "mcp.mobius.waila:Waila:${config.waila.version}:dev"
	compile("com.enderio:EnderIO:${config.enderio.version}:dev") {
		exclude module: "appliedenergistics2"
		exclude module: "buildcraft"
		exclude module: "Waila"
		exclude module: "forestry_1.7.10"
		exclude module: "OpenComputers"
		exclude module: "CodeChickenLib"
		exclude module: "CodeChickenCore"
		exclude module: "EquivalentExchange3"
	}
	compile("net.sengir.forestry:forestry_1.7.10:${config.forestry.version}:dev") {
		exclude module: "buildcraft"
		exclude module: "industrialcraft-2"
	}
	compile name: "buildcraft", version: "${config.buildcraft.version}", classifier: "dev"
	compile("appeng:appliedenergistics2:${config.ae2.version}:dev") {
		transitive = false
	}
	compile("riskyken.armourersWorkshop:Armourers-Workshop:1.7.10-${config.am.version}:deobf"){
		transitive = false
	}
	//provided("com.bluepowermod:BluePower:${config.bluepower.version}:deobf") {
	//	transitive = false
	//}
	//provided("qmunity:QmunityLib:${config.qmunitylib.version}:deobf") {
	//	transitive = false
	//}
	provided "de.dfki.mary:marytts-runtime:5.1.2:jar-with-dependencies"
	provided "de.dfki.mary:marytts-lang-en:5.1.2"
	provided "de.dfki.mary:voice-cmu-slt-hsmm:5.1.2"
}

minecraft {
	version = "${config.minecraft.version}-${config.forge.version}"
	runDir = "eclipse"

	replaceIn "pl/asie/computronics/Computronics.java"
	replace "@VERSION@", "${config.computronics.version}"
}

sourceSets {
	main {
		output.resourcesDir = 'build/classes/main'
	}
}

apply plugin: 'idea'
idea {
	module {
		scopes.PROVIDED.plus += [configurations.provided]
		scopes.PROVIDED.plus += [configurations.provided2]
		outputDir = file('build/classes/main')
	}
}

processResources {
	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		// replace version and mcversion
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	// Move access transformer to META-INF
	rename '(.+_at.cfg)', 'META-INF/$1'
}

def commonManifest = {
	attributes FMLAT: "tts_at.cfg"
}

//For Building

jar {
	exclude 'cofh/**'
	manifest commonManifest
}

def libDir = './build/libs'

task deobfJar(type: Jar) {
	description = 'Creates a jar file containing the non-obfuscated class files'
	classifier = 'deobf'
	destinationDir = file(libDir)
	from sourceSets.main.output.classesDir
	exclude 'cofh/**'
	manifest commonManifest
}

task apiZip(type: Zip) {
	description = 'Creates a zip file containing the API source files only'
	classifier = 'api'
	destinationDir = file(libDir)
	from sourceSets.main.allSource
	include 'pl/asie/**/api/**'
}

task makeJars << {
	description = 'Creates the mod files'
}

makeJars.dependsOn clean, build, deobfJar, apiZip
build.shouldRunAfter clean
deobfJar.shouldRunAfter build
apiZip.shouldRunAfter deobfJar
